if (rootProject.ext.isRelease) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}


android {
    compileSdkVersion rootProject.ext.androidId["compileSdkVersion"]
    buildToolsVersion rootProject.ext.androidId["buildToolsVersion"]

    defaultConfig {
        if (!rootProject.ext.isRelease) {
            applicationId rootProject.ext.appId["chat"]
        }

        minSdkVersion rootProject.ext.androidId["minSdkVersion"]
        targetSdkVersion rootProject.ext.androidId["targetSdkVersion"]
        versionCode rootProject.ext.androidId["versionCode"]
        versionName rootProject.ext.androidId["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            if (rootProject.ext.isRelease) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '**/debug/**'
                }
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
    }

    //use legacy for android 6.0
    useLibrary 'org.apache.http.legacy'

    //自3.6.0开始需要java8的支持
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation rootProject.ext.dependencies["appcompat"]
    implementation rootProject.ext.dependencies["constraintlayout"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["androidx-junit"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation project(":commond")
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    implementation rootProject.ext.dependencies["arouter-api"]

    implementation rootProject.ext.dependencies["play-services-base"]
    implementation rootProject.ext.dependencies["firebase-messaging"]

}
